group 'com.yelixian.beiqi'
version '0.1.0'

apply plugin: 'java'
sourceCompatibility = 1.8

processResources {
    exclude '*.proto'
}

apply plugin: 'com.google.protobuf'

buildscript {
    repositories {
        maven {
            // The google mirror is less flaky than mavenCentral()
            url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
        }
    }
    dependencies {
        // gradle versions
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.5'
    }
}

repositories {
    maven {
        // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
    }
    mavenLocal()
}

def grpcVersion = '1.12.0'
def nettyTcNativeVersion = '2.0.8.Final'

dependencies {
    compile "com.google.api.grpc:proto-google-common-protos:1.11.0"
    compile "io.grpc:grpc-alts:${grpcVersion}"
    compile "io.grpc:grpc-netty:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"

    // Used for TLS
    compile "io.netty:netty-tcnative-boringssl-static:${nettyTcNativeVersion}"

    compile "org.springframework:spring-context:4.3.17.RELEASE"
    compile "org.springframework:spring-jdbc:4.3.17.RELEASE"

    compile "mysql:mysql-connector-java:5.1.46"
    compile "com.jolbox:bonecp:0.8.0.RELEASE"
    compile "org.mybatis:mybatis:3.4.6"
    compile "org.mybatis:mybatis-spring:1.3.2"

    compile "org.slf4j:slf4j-log4j12:1.7.25"
    compile "log4j:log4j:1.2.17"

    testCompile "io.grpc:grpc-testing:${grpcVersion}"
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:1.9.5"
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

apply plugin: 'idea'
apply plugin: 'application'

startScripts.enabled = false

task beiqiServer(type: CreateStartScripts) {
    mainClassName = 'com.yelixian.beiqi.web.BeiqiServer'
    applicationName = 'beiqi-server'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into('bin') {
    from(beiqiServer)
    fileMode = 0755
}

task pack(dependsOn: [clean, installDist]) {
    copy {
        from "src/main/proto"
        into "$buildDir/resources/proto"
    }
}